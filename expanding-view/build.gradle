apply plugin: 'com.android.library'

ext {
    bintrayRepo = 'maven'
    bintrayName = 'expanding-view'

    publishedGroupId = 'com.diegodobelo.expandingview'
    libraryName = 'ExpandingView'
    artifact = 'expanding-view'

    libraryDescription = 'Android library to create expandable list with animation.'

    siteUrl = 'https://github.com/diegodobelo/AndroidExpandingViewLibrary'
    gitUrl = 'https://github.com/diegodobelo/AndroidExpandingViewLibrary.git'

    libraryVersion = '0.9.4'

    developerId = 'diegodobelo'
    developerName = 'Diego Bezerra'
    developerEmail = 'diego.bezerra@gmail.com'

    licenseName = 'ISC'
    licenseUrl = 'https://opensource.org/licenses/ISC'
    allLicenses = ["ISC"]
}

android {
    signingConfigs {
        release {}
    }
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 29
        versionCode 94
        versionName "0.9.4"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            debuggable false
        }
        debug {
            signingConfig signingConfigs.release
            debuggable true
        }
    }

    tasks.withType(Javadoc).all { enabled = false }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.13'
    implementation 'androidx.appcompat:appcompat:1.2.0-rc01'
    implementation 'com.maltaisn:icondialog:2.5.0'
}

apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/installv1.gradle'
apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/bintrayv1.gradle'

allprojects {
    afterEvaluate { project ->
        def propsFile = rootProject.file('keystore.properties')
        def configName = 'release'

        if (propsFile.exists() && android.signingConfigs.hasProperty(configName)) {
            def props = new Properties()
            props.load(new FileInputStream(propsFile))
            android.signingConfigs[configName].storeFile = file(props['storeFile'])
            android.signingConfigs[configName].storePassword = props['storePassword']
            android.signingConfigs[configName].keyAlias = props['keyAlias']
            android.signingConfigs[configName].keyPassword = props['keyPassword']
        }
    }
}